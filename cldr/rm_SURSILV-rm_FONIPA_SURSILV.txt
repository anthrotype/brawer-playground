# Transforms Rumantsch Sursilvan (rm-sursilv) to IPA (rm-fonipa-sursilv).

# References:
#
# [1] https://als.wikipedia.org/wiki/Surselvisch#Orthographi
#     with links to sound recordings on Wikimedia Commons

::Lower;

ai → aɪ̯ ;
au → aʊ̯ ;
a → a ;
b → b ;
{c} [ei] → t͡s ;
c → k ;
d → d ;
ei → ɛɪ̯ ;  # can also be /aɪ̯/ or /ɔɪ̯/ in some regions
eu → ɛʊ̯ ;
e → e ;  # can also be /ɛ/; needs a dictionary
é → e ;
è → ɛ ;
f → f ;
ge → ɟ ;
gh → g ;
gi → ɟ ;
{gl} [aeou] → gl ;
{gl} → ʎ ;
gn → ɲ ;
g → g ;
h → ;
iau → ɪa̯ʊ̯ ;
ia → ɪa̯ ;
ie → ɪɛ̯ ;
iu → ɪʊ̯ ;
i → i ;
j → j ;
k → k ;
l → l ;
m → m ;
n → n ;
o → ɔ ;
p → p ;
q → k ;
r → r ;
{sch} [aeiou] → ʒ ;  # can also be /ʃ/; needs a dictionary
{sch} → ʃ ;  # can also be /ʒ/; needs a dictionary
{s} [cptnm] → ʃ ;
{s} [gbdv] → ʒ ;
s → s ;  # can also be /z/; needs a dictionary
tg → c ;
tsch → t͡ʃ ;
t → t ;
uau → ʊa̯ʊ̯ ;
ua → ʊa̯ ;
uei → ʊɛ̯ɪ̯ ;
ue → ʊɛ̯ ;
uo → ʊɔ̯ ;
u → u ;
v → v ;
w → v ;
x → ks ;
y → i ;
z → t͡s ;
